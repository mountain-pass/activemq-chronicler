buildscript {
	repositories {
		if (project.hasProperty('wrrepousername')) {
			maven {
				credentials {
					username "${wrrepousername}"
					password "${wrrepopassword}"
				}
				url "https://windyroad.artifactoryonline.com/windyroad/repo"
			}
		}
		else {
			mavenCentral()
		}
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies { classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7' }
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'propdeps'
	apply plugin: 'propdeps-maven'
	apply plugin: 'propdeps-idea'
	apply plugin: 'propdeps-eclipse'
	apply plugin: 'project-report'
}

jar { baseName = 'activemq-chronicler' }

group = "au.com.mountain_pass"
version = 1

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	if (project.hasProperty('wrrepousername')) {
		maven {
			credentials {
				username "${wrrepousername}"
				password "${wrrepopassword}"
			}
			url "https://windyroad.artifactoryonline.com/windyroad/repo"
		}
	}
	else {
		mavenCentral()
	}
}

if (project.hasProperty('wrrepousername')) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "https://windyroad.artifactoryonline.com/windyroad/rel") {
					authentication(userName: "${wrrepousername}", password: "${wrrepopassword}")
				}
				snapshotRepository(url: "https://windyroad.artifactoryonline.com/windyroad/snaps") {
					authentication(userName: "${wrrepousername}", password: "${wrrepopassword}")
				}
			}
		}
	}
}

dependencies {
	provided('org.apache.activemq:activemq-broker:5.11.1')

	compile('net.openhft:chronicle:3.4.3')

	testCompile('org.apache.activemq:activemq-kahadb-store:5.11.1')
	testCompile('info.cukes:cucumber-java:1.2.2')
	testCompile('info.cukes:cucumber-junit:1.2.2')
	testCompile('info.cukes:cucumber-spring:1.2.2')
	testCompile('org.springframework:spring-beans:4.1.5.RELEASE')
	testCompile('org.springframework:spring-context:4.1.5.RELEASE')
	testCompile('org.springframework:spring-test:4.1.5.RELEASE')
	testCompile('org.springframework:spring-jms:4.1.5.RELEASE')
	testCompile('org.apache.activemq:activemq-spring:5.11.1')
}


eclipse {
	classpath {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
	}
}

task wrapper(type: Wrapper) { gradleVersion = '2.3' }

test {
	ext.outputDir = System.getenv("CIRCLE_TEST_REPORTS") ?: buildDir
	outputs.dir file(outputDir)
	systemProperties 'cucumber.options':  "--plugin json:${outputDir}/cucumber/activemq-chronicler.cucumber --plugin html:${outputDir}/html --plugin junit:${outputDir}/TEST-activemq-chronicler.xml"
}

test.ignoreFailures = true
